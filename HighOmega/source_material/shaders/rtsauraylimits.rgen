/*
	Copyright Â© 2023 TooMuchVoltage Software Inc. This notice shall always be coupled with any SauRay(TM) implementation and must be redistributed alongside it.

	This implementation of US20220219086A1 is provided royalty free for either of the following:

	* Games with gross revenues of under one(1) million dollars CAD.
	* Games with at least a publically distributed moddable server binary with which SauRay(TM) is successfully integrable.

	Public distribution requires either a public download link or a relatively simple registration and download process. If you are unsure of your registration process's straightforwardness, reach out directly.

	Free open-source games (i.e. Cube/Sauerbraten or Xonotic) automatically qualify since successful SauRay(TM) integration is ultimately feasible with sufficient effort.

	Open source games with non-Libre licenses (i.e. non-GPL, non-MIT) also qualify as long as the license is no further restrictive than that of Quake(idTech) II's. If unsure of whether your source code redistribution license is permissive enough, please reach out directly.

	For games where at least the distributed server component is either open-source or moddable (in a manner permissible by the IP owner) the game must be sufficiently thin-client so that a SauRay(TM) integration does not result in crashes or defects that largely break the game in most multiplayer game modes. If you are unsure of whether your distributed binaries qualify for this category, please get in touch directly.

	We can be reached at the email address: sauray@toomuchvoltage.com or using the contact information found on the website http://sauray.tech .

	If your game does not qualify under either of the above categories, contact us for a commercial license. The covered source files are protected by copyright and the aforementioned terms will apply beyond the life of US20220219086A1.

	All games using US20220219086A1 or this implementation of it must clearly declare that they're using it in a way noticeable and comprehensible by an average player of the game in the English language.

	Beyond what is stated in http://toomuchvoltage.com/pub/sauray_techbrief/sauray_techbrief.pdf this source code does not provide any warranties of merchantability or fitness for any particular purpose.
*/

#version 460
#extension GL_EXT_ray_tracing : require
#extension GL_EXT_scalar_block_layout : require

layout (binding = 0, set = 0) uniform accelerationStructureEXT topLevelAS;

struct PlayerFrustum
{
	vec4 eyeGeomRad;
	vec4 eye2Whr;
	uvec4 lookUpLook2Up2;
	vec4 geomCentYScale;
	uint maskEnabledReserved;
};

layout (scalar, binding = 1) buffer playersInfoSSBO
{
	PlayerFrustum frusta[];
} playersInfo;

struct playerLimit
{
	float aabbLim[20];
	float corners[24];
};

layout (binding = 2) buffer playerLimitsSSBO
{
	playerLimit limits[];
} playerLimits;

layout(location = 0) rayPayloadEXT struct {
	float dist;
} rayLoad;

void main()
{
	uint curPlayerId = gl_LaunchIDEXT.x / 26;

	if ( bitfieldExtract(playersInfo.frusta[curPlayerId].maskEnabledReserved, 16, 8) == 0 ) return;
	
	uint curRayId = gl_LaunchIDEXT.x % 26;
	
	if ( curRayId < 18 )
	{
		vec3 eye1 = mix (playersInfo.frusta[curPlayerId].eyeGeomRad.xyz, playersInfo.frusta[curPlayerId].eye2Whr.xyz, float (curRayId / 6) * 0.5);
		vec3 eye2;
		float rayLen;
		
		switch (curRayId % 6)
		{
			case 0:
				eye2 = eye1 + vec3 (-32.0, 0.0, 0.0);
				rayLen = 32.0;
				break;
			case 1:
				eye2 = eye1 + vec3 (32.0, 0.0, 0.0);
				rayLen = 32.0;
				break;
			case 2:
				eye2 = eye1 + vec3 (0.0, -32.0, 0.0);
				rayLen = 32.0;
				break;
			case 3:
				eye2 = eye1 + vec3 (0.0, 32.0, 0.0);
				rayLen = 32.0;
				break;
			case 4:
				eye2 = eye1 + vec3 (0.0, 0.0, -60.0);
				rayLen = 60.0;
				break;
			case 5:
				eye2 = eye1 + vec3 (0.0, 0.0, 32.0);
				rayLen = 32.0;
				break;
		}
		
		uint rayFlags = gl_RayFlagsCullNoOpaqueEXT; // Fetch extents against the map only
		uint cullMask = 0xFFFFFFFF;
		float tmin = 0.1;
		float tmax = 1.0;

		traceRayEXT(topLevelAS, rayFlags, cullMask, 0 , 0 , 0 , eye1, tmin, (eye2 - eye1), tmax, 0 );

		playerLimits.limits[curPlayerId].aabbLim[curRayId] = rayLoad.dist * rayLen;
	}
	else
	{
		curRayId -= 18;

		vec3 eye1 = playersInfo.frusta[curPlayerId].eyeGeomRad.xyz;
		vec3 eye2;

		float rayLen;
		switch (curRayId)
		{
			case 0:
				eye1 += vec3 (16.0, 16.0, 0.0);
				eye2 = eye1 + vec3 (0.0, 0.0, 32.0);
				rayLen = 32.0;
				break;
			case 1:
				eye1 += vec3 (16.0, -16.0, 0.0);
				eye2 = eye1 + vec3 (0.0, 0.0, 32.0);
				rayLen = 32.0;
				break;
			case 2:
				eye1 += vec3 (-16.0, 16.0, 0.0);
				eye2 = eye1 + vec3 (0.0, 0.0, 32.0);
				rayLen = 32.0;
				break;
			case 3:
				eye1 += vec3 (-16.0, -16.0, 0.0);
				eye2 = eye1 + vec3 (0.0, 0.0, 32.0);
				rayLen = 32.0;
				break;
			case 4:
				eye1 += vec3 (16.0, 16.0, 0.0);
				eye2 = eye1 + vec3 (0.0, 0.0, -28.0);
				rayLen = 28.0;
				break;
			case 5:
				eye1 += vec3 (16.0, -16.0, 0.0);
				eye2 = eye1 + vec3 (0.0, 0.0, -28.0);
				rayLen = 28.0;
				break;
			case 6:
				eye1 += vec3 (-16.0, 16.0, 0.0);
				eye2 = eye1 + vec3 (0.0, 0.0, -28.0);
				rayLen = 28.0;
				break;
			case 7:
				eye1 += vec3 (-16.0, -16.0, 0.0);
				eye2 = eye1 + vec3 (0.0, 0.0, -28.0);
				rayLen = 28.0;
				break;
		}

		uint rayFlags = gl_RayFlagsCullNoOpaqueEXT; // Fetch extents against the map only
		uint cullMask = 0xFFFFFFFF;
		float tmin = 0.1;
		float tmax = 1.0;
		vec3 dirVec = eye2 - eye1;

		traceRayEXT(topLevelAS, rayFlags, cullMask, 0 , 0 , 0 , eye1, tmin, dirVec, tmax, 0);
		
		eye2 = eye1 + normalize (dirVec) * max (rayLoad.dist * rayLen - 2.0, 0.0);
		
		eye1 = eye2;
		switch (curRayId % 4)
		{
			case 0:
				eye2 += vec3 (16.0, 16.0, 0.0);
				break;
			case 1:
				eye2 += vec3 (16.0, -16.0, 0.0);
				break;
			case 2:
				eye2 += vec3 (-16.0, 16.0, 0.0);
				break;
			case 3:
				eye2 += vec3 (-16.0, -16.0, 0.0);
				break;
		}
		rayLen = 22.62;

		rayFlags = gl_RayFlagsCullNoOpaqueEXT; // Fetch extents against the map only
		cullMask = 0xFFFFFFFF;
		tmin = 0.1;
		tmax = 1.0;
		dirVec = eye2 - eye1;

		traceRayEXT(topLevelAS, rayFlags, cullMask, 0 , 0 , 0 , eye1, tmin, dirVec, tmax, 0);
		
		eye2 = eye1 + normalize (dirVec) * max (rayLoad.dist * rayLen - 2.0, 0.0);
		
		playerLimits.limits[curPlayerId].corners[curRayId * 3] =  eye2.x;
		playerLimits.limits[curPlayerId].corners[curRayId * 3 + 1] =  eye2.y;
		playerLimits.limits[curPlayerId].corners[curRayId * 3 + 2] =  eye2.z;
	}
}